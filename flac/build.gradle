apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'

android {

    compileSdkVersion versions.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        versionCode versions.versionCode
        versionName versions.versionName
        consumerProguardFiles 'proguard-rules.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

// make sure that flac is set up before ndk compiling starts
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(prepareFlac)
}

task prepareFlac {
    File source = new File("$projectDir/src/main/jni/flac-1.3.2.tar.xz")
    if (!source.exists()) {
        download {
            src "https://ftp.osuosl.org/pub/xiph/releases/flac/flac-1.3.2.tar.xz"
            dest source
        }
    }
    exec {
        executable 'sh'
        args '-c', "tar xf \"${source.absolutePath}\" -C \"${source.parentFile.absolutePath}\""
    }
    File flacDestination = new File("$projectDir/src/main/jni/flac")
    new File("$projectDir/src/main/jni/flac-1.3.2").renameTo(flacDestination)
}

dependencies {
    api libraries.exoPlayer
}

clean {
    delete ".externalNativeBuild/"
}
