// Copyright (C) 2018 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
ext.fixJavadoc = {
  def javadocPath = "${project.buildDir}/docs/javadoc"
  // Fix external Android links to target the top frame.
  def androidRoot = "https://developer.android.com/reference/"
  def androidLink = "<a href=\"(${androidRoot}.*?)\\?is-external=true\""
  def androidFixed = "<a href=\"\\1\" target=\"_top\""
  ant.replaceregexp(match:androidLink, replace:androidFixed, flags:'g') {
    fileset(dir: "${javadocPath}", includes: "**/*.html")
  }
  // Fix external Oracle links to use frames and target the top frame.
  def oracleRoot = "https://docs.oracle.com/javase/7/docs/api/"
  def oracleLink = "<a href=\"(${oracleRoot})(.*?)\\?is-external=true\""
  def oracleFixed = "<a href=\"\\1index.html\\?\\2\" target=\"_top\""
  ant.replaceregexp(match:oracleLink, replace:oracleFixed, flags:'g') {
    fileset(dir: "${javadocPath}", includes: "**/*.html")
  }
  // Add favicon to each page
  def headTag = "<head>"
  def headTagWithFavicon = "<head>" +
    "<!-- start favicons snippet, use https://realfavicongenerator.net/ -->" +
    "<link rel=\"apple-touch-icon\" sizes=\"180x180\" href=\"/assets/apple-touch-icon.png\">" +
    "<link rel=\"icon\" type=\"image/png\" sizes=\"32x32\" href=\"/assets/favicon-32x32.png\">" +
    "<link rel=\"icon\" type=\"image/png\" sizes=\"16x16\" href=\"/assets/favicon-16x16.png\">" +
    "<link rel=\"manifest\" href=\"/assets/site.webmanifest\">" +
    "<link rel=\"mask-icon\" href=\"/assets/safari-pinned-tab.svg\" color=\"#fc4d50\">" +
    "<link rel=\"shortcut icon\" href=\"/assets/favicon.ico\">" +
    "<meta name=\"msapplication-TileColor\" content=\"#ffc40d\">" +
    "<meta name=\"msapplication-config\" content=\"/assets/browserconfig.xml\">" +
    "<meta name=\"theme-color\" content=\"#ffffff\">" +
    "<!-- end favicons snippet -->"
  ant.replaceregexp(match:headTag, replace:headTagWithFavicon, flags:'g') {
    fileset(dir: "${javadocPath}", includes: "**/*.html")
  }
  // Remove date metadata that changes every time Javadoc is generated.
  def javadocGeneratedBy = "<!-- Generated by javadoc.*?-->\n"
  ant.replaceregexp(match:javadocGeneratedBy, replace:"") {
    fileset(dir: "${javadocPath}", includes: "**/*.html")
  }
  def dateMeta = "<meta name=\"date\".*?>\n"
  ant.replaceregexp(match:dateMeta, replace:"") {
    fileset(dir: "${javadocPath}", includes: "**/*.html")
  }
}
